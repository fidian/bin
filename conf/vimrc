if has("autocmd")
    " Make *.less appear as "less"
    autocmd BufRead,BufNewFile *.less set filetype=less

    " Enable bundles/plugins
    runtime! autoload/pathogen.vim

    if exists("g:loaded_pathogen")
        call pathogen#infect()
    endif
endif

" Turn on syntax highlighting and modelines
if has("syntax")
	syntax on
    set modeline
endif

" Set some colors - http://vimdoc.sourceforge.net/htmldoc/syntax.html#E421
" To test:  :runtime syntax/hitest.vim
" To list:  :hi [Group - optional]
colorscheme koehler
" highlight Search term=underline cterm=underline ctermfg=green ctermbg=darkblue gui=NONE guifg=white guibg=yellow
highlight Comment cterm=NONE ctermfg=cyan gui=NONE guifg=cyan
" highlight Visual cterm=bold ctermfg=black ctermbg=gray
highlight StatusLine cterm=NONE ctermfg=yellow ctermbg=blue gui=bold guifg=yellow guibg=blue
" highlight String ctermfg=red
 highlight Normal cterm=NONE ctermfg=white guifg=white
" highlight MatchParen term=underline,bold cterm=underline,bold ctermbg=NONE
highlight ErrorMsg term=standout cterm=bold ctermfg=white ctermbg=darkred guifg=Black guibg=Red

" GUI options
set gfn=Anonymous\ Pro\ 14
" colorscheme torte

" Set tab and indent preferences
set expandtab tabstop=4 shiftwidth=4 softtabstop=4 smartindent autoindent

if has("syntax")
	" Stop the annoying behavior of '#' as the first character going to column 1
	inoremap # X<BS>#
endif

" Searching options
set incsearch ignorecase smartcase hlsearch

" Show matching brackets for a short time
set showmatch matchtime=2
set laststatus=2

" Allow backspace for everything
set backspace=indent,eol,start

if has("extra_search")
	" Clear the last search when opening a file
	nohlsearch

	" Unhighlight your search automatically when editing
	" Alternately, the following map will clear your search
	" map \ :noh<cr>
	nnoremap a :noh<CR>a
	nnoremap A :noh<CR>A
	nnoremap i :noh<CR>i
	nnoremap I :noh<CR>I
	nnoremap o :noh<CR>o
	nnoremap O :noh<CR>O
endif

if has("user_commands")
	" Allow F1 to exit edit mode.  Can bring up help, but can also remove help
	inoremap <F1> <Esc>
	noremap <F1> :call MapF1()<CR>

	function! MapF1()
		if &buftype == "help"
			exec 'quit'
		else
			exec 'help'
		endif
	endfunction
endif

" Report after N lines changed; default is two
set report=2

" Start up on the last line we were editing
if has("autocmd")
	autocmd BufReadPost * if line("'\"") > 0 && line("'\"") <= line("$") | exe "normal g`\"" | endif
endif

" Have search results appear in the middle of the screen
nmap n nzzzv
nmap N Nzzzv
nmap * *zzzv
nmap # #zzzv
nmap g* g*zzzv
nmap g# g#zzzv

" Search for "tags" file in current, then through parent directories
set tags=tags;/

" Single-key macros
nmap Q oecho __FILE__.':'.__LINE__."\n";//NOCOMMIT-DEBUG<esc>

if has("autocmd")
	" Configure Syntastic syntax checking
	map <F5> :SyntasticToggleMode<CR>
	let g:syntastic_auto_jump=1
	let g:syntastic_auto_loc_list=1
    let g:syntastic_check_on_wq=0
	let g:syntastic_javascript_jslint_args="--es5=false"
    let g:syntastic_javascript_checkers=['eslint']
    let g:syntastic_javascript_eslint_exec='eslint_d'
    let g:syntastic_typescript_checkers=['tsuquyomi', 'tslint']
    let g:syntastic_html_checkers=['']

	" Insert <Tab> or complete identifier
	" if the cursor is after a keyword character
	function MyTabOrComplete()
		let col = col('.')-1
		if !col || getline('.')[col-1] !~ '\k'
			return "\<tab>"
		else
			return "\<C-N>"
		endif
	endfunction
	inoremap <Tab> <C-R>=MyTabOrComplete()<CR>

	" Highlight SQL, html syntax in strings
	let php_sql_query=1
	let php_htmlInStrings=1
endif

" Keep the cursor 5 lines away from the top/bottom
set scrolloff=2

" Handle mistyped commands
if has("autocmd")
	command W w
	command Wq wq
	command Q q
endif

" Spell checking toggle with S
map S :setlocal spell! spelllang=en_us<CR>

if has("autocmd")
	" Automatically set up per-filetype settings, like tag matching
	filetype plugin indent on
	runtime macros/matchit.vim

	" Automatically use spaces for YAML
	autocmd Filetype yaml setlocal expandtab

    " Turn on spell checking for Markdown
    autocmd Filetype markdown setlocal spell! spelllang=en_us
endif

" Turn off search wrapping
set nowrapscan

" Set a custom status line
" %< = trim from left, %f = filename, %h = [help], %r = [RO]
" %= = right align, %b = char decimal, %B = char hex
" %c = column number, %V = virtual column number,
" %l = line, %L = total lines, %P = percentage through file
set statusline=%<%f\ %h%m%r%=%b\ 0x%B\ \ \ c%c%V\ +%l/%L\ %P

" Make a copy when making a backup file, then overwrite original
" Works around karma issue #974
set backupcopy=yes

" Highlight characters that cause problems
" \u00a0  hard space, non-breaking space
" \u1680  Ogham space mark (usually read as a dash)
" \u180e  Mongolian vowel separator (no width)
" \u2000  en quad
" \u2001  em quad
" \u2002  en space
" \u2003  em space
" \u2004  three-per-em space
" \u2005  four-per-em space
" \u2006  six-per-em space
" \u2007  figure space
" \u2008  punctuation space
" \u2009  thin space
" \u200a  hair space
" \u200b  zero width space
" \u2014  hyphen (not really whitespace)
" \u202f  narrow non-breaking space
" \u205f  medium mathematical space
" \u3000  ideographic space
" \uffff  zero width non-breaking space
highlight ErrorCharacters ctermbg=red guibg=red
match ErrorCharacters "[\u00a0\u1680\u180e\u2000-\u200b\u2014\u202f\u205f\u3000\uffff]"

" Fold and unfold by using Control-Space.  vim in console sees
" it as Control-@
inoremap <C-Space> <C-O>za
nnoremap <C-Space> za
onoremap <C-Space> <C-C>za
vnoremap <C-Space> zf
inoremap <C-@> <C-O>za
nnoremap <C-@> za
onoremap <C-@> <C-C>za
vnoremap <C-@> zf

" Grammar checking options.  Enable grammar checking by using
" `:GrammarousCheck` and turn it off with `:GrammarousReset`.
let g:grammarous#default_comments_only_filetypes={
    \ 'javascript': 1,
    \ 'less': 1,
    \ 'vim': 1,
    \ '*' : 0
    \ }
let g:grammarous#hooks = {}
function! g:grammarous#hooks.on_check(errs)
    nmap <buffer><C-n> <Plug>(grammarous-move-to-next-error)
    nmap <buffer><C-p> <Plug>(grammarous-move-to-previous-error)
endfunction
function! g:grammarous#hooks.on_reset(errs)
    nunmap <buffer><C-n>
    nunmap <buffer><C-p>
endfunction

" Automatically format Terraform and other HCL files
let g:terraform_fmt_on_save = 1

" Control-letter moves to another split
nnoremap <silent> <C-L> <c-w>l
nnoremap <silent> <C-H> <c-w>h
nnoremap <silent> <C-K> <c-w>k
nnoremap <silent> <C-J> <c-w>j

" Control-arrow resizes splits
nnoremap <silent> <C-Right> <c-w>>
nnoremap <silent> <C-Left> <c-w><
nnoremap <silent> <C-Down> <c-w>+
nnoremap <silent> <C-Up> <c-w>-
